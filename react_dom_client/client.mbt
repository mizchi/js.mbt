///|
#external
pub type ReactDOMRoot

///|
pub fn ReactDOMRoot::as_any(self : ReactDOMRoot) -> @core.Any = "%identity"

///|
#module("react-dom/client")
extern "js" fn ffi_create_root(container : @dom.Element) -> ReactDOMRoot = "createRoot"

///|
#module("react-dom/client")
extern "js" fn ffi_hydrate_root(
  dom_node : @dom.Element,
  react_node : @core.Any,
) -> ReactDOMRoot = "hydrateRoot"

///|
/// Create a React root for displaying content in a DOM element.
#alias(create_root)
pub fn createRoot(container : @dom.Element) -> ReactDOMRoot {
  ffi_create_root(container)
}

///|
pub fn ReactDOMRoot::render(self : ReactDOMRoot, vdom : @react.Element) -> Unit {
  let vdom_js : @core.Any = vdom.as_any().cast()
  self.as_any()._call("render", [vdom_js]) |> ignore
}

///|
/// Hydrate a server-rendered React application
/// hydrateRoot lets you display React components inside a browser DOM node
/// whose HTML content was previously generated by react-dom/server.
#alias(hydrate_root)
pub fn hydrateRoot(
  dom_node : @dom.Element,
  react_node : @react.Element,
) -> ReactDOMRoot {
  let react_node_js : @core.Any = react_node.as_any().cast()
  ffi_hydrate_root(dom_node, react_node_js)
}

///|
pub fn ReactDOMRoot::unmount(self : ReactDOMRoot) -> Unit {
  self.as_any()._call("unmount", []) |> ignore
}
