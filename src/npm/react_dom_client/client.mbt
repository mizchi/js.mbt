///|
#external
pub type ReactDOMClient

///|
#external
pub type ReactDOMRoot

///|
/// Dynamic import for react-dom/client module (internal)
/// NOTE: This uses dynamic import which doesn't support tree-shaking.
/// When MoonBit adds native ESM support, this should be replaced with static imports.
extern "js" fn import_react_dom_client_internal() -> @js.Promise[ReactDOMClient] =
  #| () => import("react-dom/client")

///|
/// Dynamic import for react-dom/client module
/// NOTE: This uses dynamic import which doesn't support tree-shaking.
/// When MoonBit adds native ESM support, this should be replaced with static imports.
pub async fn dynamic_import() -> ReactDOMClient {
  import_react_dom_client_internal().wait()
}

///|
pub fn ReactDOMClient::as_any(self : ReactDOMClient) -> @nostd.Any = "%identity"

///|
#alias(create_root)
pub fn ReactDOMClient::createRoot(
  self : Self,
  container : &@dom.ElementImpl,
) -> ReactDOMRoot {
  self.as_any()._call("createRoot", [container.as_any()]).cast()
}

///|
pub fn ReactDOMRoot::as_any(self : ReactDOMRoot) -> @nostd.Any = "%identity"

///|
pub fn ReactDOMRoot::render(self : ReactDOMRoot, vdom : @react.Element) -> Unit {
  let vdom_js : @nostd.Any = vdom.as_any().cast()
  self.as_any()._call("render", [vdom_js]) |> ignore
}

///|
/// Hydrate a server-rendered React application
/// hydrateRoot lets you display React components inside a browser DOM node
/// whose HTML content was previously generated by react-dom/server.
#alias(hydrate_root)
pub fn ReactDOMClient::hydrateRoot(
  self : Self,
  dom_node : &@dom.ElementImpl,
  react_node : @react.Element,
) -> ReactDOMRoot {
  let react_node_js : @nostd.Any = react_node.as_any().cast()
  self.as_any()._call("hydrateRoot", [dom_node.as_any(), react_node_js]).cast()
}

///|
pub fn ReactDOMRoot::unmount(self : ReactDOMRoot) -> Unit {
  self.as_any()._call("unmount", []) |> ignore
}
